plugins {
	id 'org.springframework.boot' version '2.5.2'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	// add
	id 'maven-publish'
	id 'jacoco'
	id 'com.diffplug.spotless' version '5.11.1'
	id 'com.gorylenko.gradle-git-properties' version '2.3.1'
	id 'org.sonarqube' version '3.3'
	id 'org.owasp.dependencycheck' version '6.2.2'
	id 'org.openapi.generator' version '5.1.1'
	// doc
	id 'org.asciidoctor.jvm.convert' version '3.1.0'
	id 'org.asciidoctor.jvm.pdf' version '3.1.0'
	id 'io.github.lhotari.swagger2markup' version '1.3.3.1'
}

group = 'com.cathay.wmsp'
version = '0.0.1'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	maven { url 'https://repo.spring.io/release' }
	mavenCentral()
}

ext {
	set('springCloudVersion', "2020.0.3")
	set('testcontainersVersion', "1.15.3")
	// add
	set('springfoxVersion', "3.0.0")
	set('mapstructVersion', "1.4.2.Final")
	set('archunitVersion', "0.18.0")
	set('lombokMapstructBindingVersion', "0.2.0")
	// OpenAPI input file
	set('openapiInputSpec', "${rootDir}/specs/openapi.yaml")
	// Output dir
	set('openapiOutputDir', "${rootDir}/generated/springboot")
	set('openapiInvokerPackage', "${group}")
	// Controller
	set('openapiApiPackage', "${openapiInvokerPackage}.${rootProject.name}.interfaces.rest")
	// Models
	set('openapiModelPackage', "${openapiInvokerPackage}.${rootProject.name}.interfaces.rest.dto")
	// JavaConfig
	set('openapiConfigPackage', "${openapiInvokerPackage}.configuration")
	set('apiDocPath', "${buildDir}/swagger/api.json")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-rest'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
	implementation 'org.liquibase:liquibase-core'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.apache.commons:commons-lang3'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'
	
	// Swagger
	implementation 'io.springfox:springfox-boot-starter:3.0.0'
	implementation 'io.springfox:springfox-swagger-ui:3.0.0'
	implementation 'io.springfox:springfox-data-rest:3.0.0'
	
	// UnitTest
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
	testImplementation "com.tngtech.archunit:archunit:${archunitVersion}"
	
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	runtimeOnly 'org.postgresql:postgresql'
}

dependencyManagement {
	imports {
		mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

test {
	useJUnitPlatform()
	systemProperty 'apiDocPath', "${apiDocPath}"
	finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
}

gitProperties {
	failOnNoGitDirectory = false
}

// ./gradlew spotlessApply
spotless {
	encoding 'UTF-8' // all formats will be interpreted as UTF-8
	java {
		target 'src/**/*.java'
		toggleOffOn()
		googleJavaFormat()
		removeUnusedImports()
		importOrder()
		trimTrailingWhitespace()
		endWithNewline()
	}
	sql {
		target 'src/**/*.sql'
		dbeaver()
	}
	groovyGradle {
		target '*.gradle'
		greclipse()
	}
	format 'xml', {
		target 'src/**/*.xml'
		eclipseWtp('xml')
	}
}

openApiGenerate {
	generatorName = "spring"
	library = "spring-boot"
	inputSpec = "${openapiInputSpec}"
	outputDir = "${openapiOutputDir}"
	invokerPackage = "${openapiInvokerPackage}"
	apiPackage = "${openapiApiPackage}"
	modelPackage = "${openapiModelPackage}"
	configOptions = [
		configPackage: "${openapiConfigPackage}",
		dateLibrary: "java8",
		openApiNullable: "false",
		disallowAdditionalPropertiesIfNotPresent: "false",
		groupId: "${group}",
		hateoas: "false",
		interfaceOnly: "true",
		licenseName: "Unlicense",
		singleContentTypes: "true", // Whether to select only one produces/consumes content-type by operation.
		title: "${rootProject.name}",
		unhandledException: "true", // Declare operation methods to throw a generic exception and allow unhandled exceptions
		useTags: "true", // use tags for creating interface and controller classnames
		useOptional: "true"
	]
}

// ./gradlew convertSwagger2markup
// 產生到 asciidoc 下 definitions.adoc overview.adoc paths.adoc security.adoc
convertSwagger2markup {
	swaggerInput file("${apiDocPath}").getAbsolutePath()
	outputFile file("${buildDir}/swagger/api")
	config = [
		'swagger2markup.markupLanguage' : 'ASCIIDOC',
		'swagger2markup.pathsGroupedBy' : 'TAGS',
		'swagger2markup.outputLanguage': 'EN',
		'swagger2markup.generatedExamplesEnabled': true,
		'swagger2markup.inlineSchemaEnabled': false
	]
}

// ./gradlew asciidoctor
asciidoctor {
	sourceDir file("${buildDir}/swagger")
	sources {
		include 'api.adoc'
	}
	attributes 'doctype': 'book',
	'icons': 'font',
	'source-highlighter': 'highlightjs',
	'toc': 'left',
	'toclevels': 2,
	'sectlinks': true
	outputOptions {
		backends = ['html5', 'pdf']
	}
	outputDir file('publish')
}
